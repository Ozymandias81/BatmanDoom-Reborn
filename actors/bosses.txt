//////////
//BOSSES//
//////////
ACTOR NinjaBoss : Ninja1 replaces FloatingSkull
{
	//$Category Reborn Bosses
	Tag "Ninja Boss"
	Health 225
	Speed 30
	Damage 6 //more damage when jumping!
	Scale 0.88
	+BOSS
	Obituary "$NINJAB"
	Translation "104:111=152:159","5:8=9:12"
	States
		{
		Death:
			BSPI J 20 A_Scream
			"####" K 7 A_NoBlocking
			"####" LMN 7
			"####" O 20
			"####" O 20 A_BrainDie
			Loop
		Burn: //new
			PLAY H 1 Bright
			"####" I 0 A_Fall
			"####" I 0 A_SpawnItem("BurnBurnBurn2",1)
			TNT1 A 60 A_BrainDie
			Stop
		Crush:
			CRS2 A -1 A_BrainDie
			Stop
		}
}

ACTOR TinyExplosion
{
	Game Doom
	Radius 16
	Height 16
	+INVISIBLE
	+OLDRADIUSDMG
	States
		{
		Spawn:
		TNT1 A 0
		TNT1 A 1 A_Explode(20)
		Stop
		}
}
//this wasn't correctly inherited with gzdoom.pk3 dehsupp - Holy shit 2 SpawnIDs XD
ACTOR CrocRock replaces CacodemonBall
{
	Game Doom
	Radius 6
	Height 8
	Speed 46
	VSpeed 1
	FastSpeed 56
	Damage 5
	Projectile
	-NOGRAVITY
	+BOUNCEONACTORS
	+DOOMBOUNCE
	DeathSound "skeleton/melee"
	States
		{
		Spawn:
			SSWV U 10
			Loop
		Death://Hit Player
			SSWV U 0
			SSWV U 0 A_SpawnItemEx("StoneFloor")
			SSWV U 0 A_SpawnItemEx("TinyExplosion")
			SSWV U 1
			Stop
		}
}

ACTOR KillerCroc replaces PainElemental
{
	//$Category Reborn Bosses
	Tag "Killer Croc"
	Health 1050
	Radius 20
	Height 88
	Mass 400
	Speed 15
	PainChance 128
	Scale 1.25
	Monster
	+AMBUSH //inactive till player LOS
	+BOSS
	+FLOORCLIP
	DeathSound "pain/death"
	PainSound "pain/pain"
	SeeSound "knight/sight"
	BloodColor "00 be 00"
	States
		{
		Spawn:
			VILE A 10 A_Look
			Loop
		See:
			VILE A 0 ACS_Execute (701, 0, 1, 0, 0)
			VILE AABBCCDDEEFF 2 A_VileChase
			GOTO See+1
		Melee:
			SKUL C 7
			SKUL C 7 A_SkelFist
			SKUL D 0
			Goto See
		Pain:
			PAIN G 6
			PAIN G 6 A_Pain
			Goto See
		Death:
			CPOS H 30 A_Scream
			CPOS I 5
			CPOS J 5 A_KeenDie
			CPOS KLM 5
			CPOS N -1
			Stop
		Burn: //new
			PLAY H 1 Bright
			PLAY I 0 A_Fall
			PLAY I 0 A_SpawnItem("BurnBurnBurn3",1)
			TNT1 A 60 A_KeenDie
			Stop
		Crush:
			CRS3 A -1 A_KeenDie
			Stop
		Heal:
			VILE "[" 6
			VILE "\" 6 A_FaceTarget
			VILE "]" 8 A_CustomMissile("CrocRock", 60, 0, 0, 1, 0)
			Goto See
		}
}

ACTOR KillerCrocWalking : KillerCroc //used only on map03
{
	Height 63
	States
		{
		See:
			VILE AABBCCDDEEFF 2 A_VileChase
			Loop
		}
}

ACTOR TallyMan replaces SpiderMastermind
{
	//$Category Reborn Bosses
	Tag "Tally Man"
	Health 1200
	Radius 24
	Height 63
	Mass 1000
	Speed 15
	PainChance 30
	Scale 0.88
	Monster
	+AMBUSH //inactive till player LOS
	+BOSS
	+DONTMORPH
	+FLOORCLIP
	+MISSILEMORE
	+NORADIUSDMG
	AttackSound "spider/attack"
	DeathSound "vile/death"
	PainSound "knight/pain"
	SeeSound "spider/sight"
	Obituary "$TALLY"
	States
		{
		Spawn:
			POSS A 10 A_Look
			Loop
		See:
			POSS A 0 ACS_Execute (701, 0, 2, 0, 0)
			POSS AABBCCDD 3 A_Chase
			GOTO See+1
		Missile:
			SPID G 15 bright A_FaceTarget
			SPID HH 4 bright A_CustomBulletAttack(0, 0, 1, 4, "BulletPuff", 0)
			Goto See
		Pain:
			POSS G 3
			POSS G 3 A_Pain
			Goto See
		Death:
			TROO I 8
			TROO J 8 A_Scream
			TROO KL 6
			TROO M 20
			TROO M -1 A_BrainDie
			Stop
		Burn: //new
			PLAY H 1 Bright
			PLAY I 0 A_Fall
			PLAY I 0 A_SpawnItem("BurnBurnBurn3",1)
			TNT1 A 60 A_BrainDie
			Stop
		Crush:
			CRS2 A -1 A_BrainDie
			Stop
		}
}

/*ACTOR CopterBoom //at the moment this actor is not used
{
	Radius 11
	Height 8
	Speed 8
	Damage 0
	Projectile
	RenderStyle Add
	+BRIGHT
	+MISSILE
	+NOINTERACTION
	States
		{
		Spawn:
			COPT A 8
			"####" B 6
			"####" C 4 A_FadeOut (0.05)
			Stop
		}
}*/

ACTOR PenguinCopter replaces TechPillar
{
	//$Category Reborn Bosses
	Tag "Penguin (Helicopter)"
	Health 1500
	Height 83
	Mass 600
	PainChance 15
	Radius 72
	Speed 8
	Monster
	+AMBUSH //inactive till player LOS
	+BOSS
	+FLOAT
	+FLOATBOB
	+FLOORCLIP
	+NOBLOOD
	+NOGRAVITY
	DeathSound "weapons/rocklx"
	Obituary "$PENGCOPT"
	States
		{
		Spawn:
			TNT1 A 0 A_Playsoundex("vile/firecrkl", CHAN_AUTO, 1, 0)
			SSWV A 10 A_Look
			Loop
		See:
			TNT1 A 0 A_Playsoundex("vile/firecrkl", CHAN_AUTO, 1, 0)
			SSWV A 0 ACS_Execute (701, 0, 3, 0, 0)
			SSWV AABB 2 A_Chase
			Goto See+1
		Pain:
			TNT1 A 0 A_Playsoundex("vile/firecrkl", CHAN_AUTO, 1, 0)
			SSWV A 1 A_Pain
			Goto See
		Missile:
			TNT1 A 0 A_Playsoundex("vile/firecrkl", CHAN_AUTO, 1, 0)
			SSWV A 2 bright A_FaceTarget
			CPOS F 2 bright light(BDPENGC1)A_SPosAttack
			SSWV A 4 bright light(BDPENGC2)A_SPosAttack
			CPOS F 2 bright light(BDPENGC1)A_SpidRefire
			Goto Missile+1
		Death:
			TNT1 A 0 A_ChangeFlag(FLOATBOB, 0)
			SPID J 0 A_SetFloorClip
			SPID J 5 A_Scream
			SPID K 5 A_NoBlocking
			TNT1 A 0 A_Playsoundex("vile/firecrkl", CHAN_AUTO, 0, 0)
			SPID LMNOPQR 5
			SPID S 60
			SPID S -1 A_BrainDie
			Stop
		}
}

ACTOR PenguinCopterSound : PenguinCopter
{
	States
		{
		See:
			SSWV A 2 A_PlaySound("vile/firecrkl")
			SSWV ABB 2 A_Chase
		}
}

ACTOR PenguinBoss2 replaces HangTLookingUp
{
	//$Category Reborn Bosses
	Tag "Penguin"
	Health 1000
	Height 183
	Mass 1000000
	PainChance 200
	Radius 24
	Speed 0
	Monster
	+AMBUSH //inactive till player LOS
	+BOSS
	+FLOORCLIP
	PainSound "fatso/shotx"
	DeathSound "brain/spit"
	Obituary "$PENGUIN"
	States
		{
		Spawn:
			BAL7 C 5 A_Look
			Loop
		See:
			BAL7 A 0 ACS_Execute (701, 0, 3, 0, 0)
			BAL7 C 5 A_Chase
			Goto See+1
		Pain:
			BAL7 D 5 A_Pain
			BAL7 CECD 5
			Goto See
		Missile:
			SSWV H 5
			BBRN B 5 Bright A_SPosAttack
			SSWV H 5
			Goto See
		Death:
			BAL7 E 5 A_Scream
			BBRN A 5
			TBLU A 6
			TBLU B 6
			TBLU C 6 A_KeenDie
			TBLU D -1
			Stop

	/*Burn: //new - these will require brand new sprites
			PLAY H 1 Bright
			PLAY I 0 A_Fall
			PLAY I 0 A_SpawnItem("BurnBurnBurn1",1)
			TNT1 A 60 A_KeenDie
			Stop
		Crush:
			CRS2 A -1 A_KeenDie
			Stop*/

			}
}

ACTOR ScarecrowShotSmoke replaces RevenantTracerSmoke
{
	Alpha 0.3
	RenderStyle Translucent
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	DamageType "Poison"
	States
		{
		Spawn:
			FIRE CBC 4 A_Explode(3,16)
			CEYE ABC 4 A_Explode(3,16)
			Stop
		}
}

ACTOR ScarecrowShotSmoke2 replaces RevenantTracerSmoke
{
	Alpha 0.3
	RenderStyle Translucent
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	States
		{
		Spawn:
			FIRE CBC 4
			FIRE CBC 4
			CEYE ABC 4
			Stop
		}
}

ACTOR ScarecrowShot2
{
	Game Doom
	Radius 11
	Height 8
	Speed 10
	Damage 5
	DamageType "Poison"
	RenderStyle Translucent
	Alpha 0.8
	Projectile
	+BRIGHT
	+RANDOMIZE
	SeeSound "wolfss/sight"
	DeathSound "skeleton/attack"
	States
		{
		Spawn:
			FATB ABAB 2
			FATB A 1 Light(ScareShot1) A_CStaffMissileSlither
			FATB A 0 Light(ScareShot2) A_CStaffMissileSlither
			FATB A 0 Light(ScareShot2) A_SpawnItemEx("ScarecrowShotSmoke2", random(-24,24), random(-24,24), random(-24,24))
			FATB A 1 Light(ScareShot3) A_CStaffMissileSlither
			FATB A 0 Light(ScareShot2) A_CStaffMissileSlither
			FATB A 0 Light(ScareShot3) A_SpawnItemEx("ScarecrowShotSmoke", random(-24,24), random(-24,24), random(-24,24))
			FATB B 1 Light(ScareShot3) A_CStaffMissileSlither
			FATB B 0 Light(ScareShot4) A_CStaffMissileSlither
			FATB A 0 Light(ScareShot4) A_SpawnItemEx("ScarecrowShotSmoke2", random(-24,24), random(-24,24), random(-24,24))
			FATB B 1 Light(ScareShot5) A_CStaffMissileSlither
			FATB B 0 Light(ScareShot4) A_CStaffMissileSlither
			FATB B 0 Light(ScareShot5) A_SpawnItemEx("ScarecrowShotSmoke", random(-24,24), random(-24,24), random(-24,24))
			Goto Spawn+4
		Death:
			FBXP A 8
			FBXP B 6
			FBXP C 4
			Stop
		}
}

ACTOR Scarecrow replaces Revenant
{
	//$Category Reborn Bosses
	Tag "Scarecrow"
	Health 1300
	Height 63
	Mass 500
	PainChance 100
	Radius 24
	Speed 10
	Scale 0.88
	Monster
	+BOSS
	+FLOORCLIP
	+LONGMELEERANGE
	+MISSILEMORE
	SeeSound "skeleton/sight"
	PainSound "knight/pain"
	DeathSound "skeleton/death"
	Obituary "$SCAREOB"
	HitObituary "$SCAREHIT"
	DamageFactor "Poison", 0.0
	DamageFactor "PoisonCloud", 0.0
	BloodColor "00 be 00"
	States
		{
		Spawn:
			BOSS A 10 A_Look
			Loop
		See:
			BOSS A 0 ACS_Execute (701, 0, 4, 0, 0)
			BOSS AABBCCDD 2 A_Chase
			Goto See+1
		Melee:
			SKEL G 0 A_FaceTarget
			SKEL G 6 A_SkelWhoosh
			SKEL H 6 A_FaceTarget
			SKEL I 6 A_SkelFist
			Goto See
		Missile:
			SKEL J 0 A_Jump(128,"Missile2")
			SKEL J 10 A_FaceTarget
			SKEL K 10 Light(ScareShot3) A_CustomMissile("ScarecrowShot2", 40)
			SKEL K 10 A_FaceTarget
			Goto See
		Missile2:
			SKEL J 10 A_FaceTarget
			SKEL K 0 Light(ScareShot3) A_CustomMissile("ScarecrowShot2", 40,-5,5)
			SKEL K 10 Light(ScareShot3) A_CustomMissile("ScarecrowShot2", 40,5,-5)
			SKEL K 10 A_FaceTarget
			Goto See
		Pain:
			SKEL L 5
			SKEL L 5 A_Pain
			Goto See
		Death:
			SKEL LM 7
			SKEL N 7 A_Scream
			SKEL Q -1 A_KeenDie
			Loop
		Burn: //new
			PLAY H 1 Bright
			PLAY I 0 A_Fall
			PLAY I 0 A_SpawnItem("BurnBurnBurn2",1)
			TNT1 A 60 A_KeenDie
			Stop
		Crush:
			CRS2 A -1 A_KeenDie
			Stop
		}
}

ACTOR TwoFaceRocket replaces BaronBall
{
	Game Doom
	Radius 6
	Height 16
	Speed 15
	FastSpeed 20
	Damage 5
	Projectile
	+BLOODLESSIMPACT
	+BRIGHT
	+RANDOMIZE
	SeeSound "baron/attack"
	DeathSound "weapons/rocklx"
	States
		{
		Spawn:
			BAL7 A 4 Light(BDFMAN1)
			Loop
		Death:
			MISL B 3 Light(BDFMAN4) A_Explode(128)
			MISL CD 3 Light(BDFMAN3)
			APBX AB 3 Light(BDFMAN2)
			Stop
		}
}

ACTOR TwoFace replaces Cyberdemon
{
	//$Category Reborn Bosses
	Tag "Two Face"
	Health 1200
	Height 63
	Mass 1000
	PainChance 128
	Radius 24
	Speed 7
	Scale 0.88
	Monster
	+AMBUSH //inactive till player LOS
	+BOSS
	+DONTMORPH
	+FLOORCLIP
	+MISSILEMORE
	+NORADIUSDMG
	DeathSound "keen/death"
	PainSound "cyber/pain"
	SeeSound "cyber/sight"
	Obituary "$TWOFACE"
	States
		{
		Spawn:
			CYBR A 10 A_Look
			Loop
		See:
			CYBR A 0 ACS_Execute (701, 0, 5, 0, 0)
			CYBR AABBCCDD 3 A_Chase
			Goto See+1
		Melee:
			BOS2 G 15 A_SkelFist
			CYBR A 10 A_FaceTarget
		Missile:
			CYBR E 0 A_FaceTarget
			CYBR F 8 BRIGHT Light(BDFOES2) A_CustomMissile("BaronBall", 70, 12)
			CYBR E 6 A_FaceTarget
			Goto See
		Pain:
			CYBR G 10 A_Pain
			Goto See
		Death:
			CYBR H 10
			CYBR I 10 A_Scream
			CYBR JKL 10
			CYBR M 10 A_NoBlocking
			CYBR NO 10
			CYBR P 30
			CYBR P -1 A_KeenDie
		Burn: //new
			PLAY H 1 Bright
			PLAY I 0 A_Fall
			PLAY I 0 A_SpawnItem("BurnBurnBurn1",1)
			TNT1 A 60 A_KeenDie
			Stop
		Crush:
			CRS2 A -1 A_KeenDie
			Stop
		}
}

ACTOR JokerTrickDart1 replaces FatShot //shaders & rotsprites?
{
	Game Doom
	Radius 6
	Height 8
	Speed 20
	FastSpeed 20
	Damage 3
	Projectile
	SeeSound "weapons/rocklf"
	DeathSound "weapons/plasmax"
	States
		{
		Spawn:
			MANF A -1
			Loop
		Death:
			PLAY UVW 8 BRIGHT
			Stop
		}
}

ACTOR JokerTrickDart2 : JokerTrickDart1 {Translation "176:191=112:127","40:47=9:12"}
ACTOR JokerTrickDart3 : JokerTrickDart1 {Translation "176:191=192:207","40:47=204:207"}

ACTOR Joker replaces ZombieMan
{
	//$Category Reborn Bosses
	Tag "Joker A"
	Health 1600
	Height 63
	Mass 1000
	PainChance 20
	Radius 24
	Speed 8
	Scale 0.88
	Monster
	+AMBUSH //inactive till player LOS
	+BOSS
	+FLOORCLIP
	AttackSound "weapons/pistol"
	DeathSound "vile/death"
	PainSound "vile/pain"
	SeeSound "pain/active"
	Obituary "$JOKER"
	States
		{
		Spawn:
			SPID A 10 A_Look
			Loop
		See:
			SPID A 0 ACS_Execute (701, 0, 6, 0, 0)
			SPID ABCDEF 3 A_Chase
			Goto See+1
		Missile:
			VILE G 0
			VILE G 0 A_JumpIfHealthLower (801, "Missile2")
		Missile1:
			VILE GHIJK 6 A_FaceTarget
		Shoot:
			VILE L 6 Bright Light(BDFOES2) A_VileAttack
			VILE K 6 A_LookEx(0, 0, 0, 0, 0, "Shoot2")
			Goto Away
		Shoot2:
			VILE KKKKKKK 5  A_FaceTarget
			VILE L 6 Bright Light(BDFOES2) A_VileAttack
			VILE K 6 A_LookEx(0, 0, 0, 0, 0, "Shoot3")
		Shoot3:
			VILE KKKKKKK 5  A_FaceTarget
			VILE L 6 Bright Light(BDFOES2) A_VileAttack
			VILE K 6
		Away:
			VILE JIH 6 A_FaceTarget
			Goto See
		Missile2:
			FATT G 20 A_FaceTarget
			FATT G 0 A_Jump(86, "Dart3")
			FATT G 0 A_Jump(128, "Dart2")
		Dart1:
			FATT G 4 A_CustomMissile(JokerTrickDart1, 44, 0, 10)
			FATT G 4 A_CustomMissile(JokerTrickDart2, 44, 0, 5)
			FATT H 4 A_CustomMissile(JokerTrickDart2, 44, 0, 0)
			FATT H 4 A_CustomMissile(JokerTrickDart3, 44, 0, -5)
			FATT H 4 A_CustomMissile(JokerTrickDart1, 44, 0, -10)
			Goto See
		Dart2:
			FATT G 4 A_CustomMissile(JokerTrickDart3, 44, 0, -15)
			FATT G 4 A_CustomMissile(JokerTrickDart1, 44, 0, -7)
			FATT H 4 A_CustomMissile(JokerTrickDart3, 44, 0, 0)
			FATT H 4 A_CustomMissile(JokerTrickDart2, 44, 0, 8)
			FATT H 4 A_CustomMissile(JokerTrickDart2, 44, 0, 15)
			Goto See
		Dart3:
			FATT G 4 A_CustomMissile(JokerTrickDart2, 44, 0, 20)
			FATT G 4 A_CustomMissile(JokerTrickDart3, 44, 0, 3)
			FATT H 4 A_CustomMissile(JokerTrickDart1, 44, 0, -3)
			FATT H 9 A_CustomMissile(JokerTrickDart3, 44, 0, -20)
			FATT H 4 A_CustomMissile(JokerTrickDart1, 44, 0, 0)
			Goto See
		Pain:
			VILE Q 5 A_Pain
			Goto See
		Death:
			KEEN AB 5
			KEEN C 5 A_Scream
			KEEN DE 5
			KEEN F -1 A_KeenDie
			Stop
		Burn: //new
			PLAY H 1 Bright
			PLAY I 0 A_Fall
			PLAY I 0 A_SpawnItem("BurnBurnBurn1",1)
			TNT1 A 60 A_KeenDie
			Stop
		Crush:
			CRS2 A -1 A_KeenDie
			Stop
		}
}

ACTOR JokerObserving : Joker
{
	//$Category Reborn Bosses
	Tag "Joker (Observing)"
	Health 800
	States
		{
		Spawn:
			SPID A 10 A_Look
			Loop
		See:
			SPID ABCDEF 3 A_Chase
			Loop
		Missile:
			VILE GHIJK 6 A_FaceTarget
			VILE L 6 Bright A_VileAttack
			VILE KJIH 6 A_FaceTarget
			Goto See
		Pain:
			VILE Q 5 A_Pain
			Goto See
		Death:
			KEEN AB 5
			KEEN C 5 A_Scream
			KEEN DE 5
			KEEN F -1 A_KeenDie
			Stop
		Burn: //new
			PLAY H 1 Bright
			PLAY I 0 A_Fall
			PLAY I 0 A_SpawnItem("BurnBurnBurn1",1)
			TNT1 A 60 A_KeenDie
			Stop
		Crush:
			CRS2 A -1 A_KeenDie
			Stop
		}
}

ACTOR Joker2 replaces Clip
{
	//$Category Reborn Bosses
	Tag "Joker B"
	Health 800
	Height 63
	Mass 1000
	PainChance 20
	Radius 24
	Speed 8
	Scale 0.88
	Monster
	+AMBUSH //inactive till player LOS
	+BOSS
	+FLOORCLIP
	DeathSound "vile/death"
	PainSound "vile/pain"
	SeeSound "pain/active"
	Obituary "$JOKER2"
	States
		{
		Spawn:
			SPID A 10 A_Look
			Loop
		See:
			SPID A 0 ACS_Execute (701, 0, 8, 0, 0)
			SPID ABCDEF 3 A_Chase
			Goto See+1
		Missile:
			FATT G 20 A_FaceTarget
			FATT G 0 A_Jump(86, "Dart3")
			FATT G 0 A_Jump(128, "Dart2")
		Dart1:
			FATT G 4 A_CustomMissile(JokerTrickDart1, 44, 0, 10)
			FATT G 4 A_CustomMissile(JokerTrickDart2, 44, 0, 5)
			FATT H 4 A_CustomMissile(JokerTrickDart2, 44, 0, 0)
			FATT H 4 A_CustomMissile(JokerTrickDart3, 44, 0, -5)
			FATT H 4 A_CustomMissile(JokerTrickDart1, 44, 0, -10)
			Goto See
		Dart2:
			FATT G 4 A_CustomMissile(JokerTrickDart3, 44, 0, -15)
			FATT G 4 A_CustomMissile(JokerTrickDart1, 44, 0, -7)
			FATT H 4 A_CustomMissile(JokerTrickDart3, 44, 0, 0)
			FATT H 4 A_CustomMissile(JokerTrickDart2, 44, 0, 8)
			FATT H 4 A_CustomMissile(JokerTrickDart2, 44, 0, 15)
			Goto See
		Dart3:
			FATT G 4 A_CustomMissile(JokerTrickDart2, 44, 0, 20)
			FATT G 4 A_CustomMissile(JokerTrickDart3, 44, 0, 3)
			FATT H 4 A_CustomMissile(JokerTrickDart1, 44, 0, -3)
			FATT H 9 A_CustomMissile(JokerTrickDart3, 44, 0, -20)
			FATT H 4 A_CustomMissile(JokerTrickDart1, 44, 0, 0)
			Goto See
		Pain:
			VILE Q 5
			VILE Q 5 A_Pain
			Goto See
		Death:
			KEEN AB 5
			KEEN C 5 A_Scream
			KEEN DE 5
			KEEN F -1 A_KeenDie
			Stop
		Burn: //new
			PLAY H 1 Bright
			PLAY I 0 A_Fall
			PLAY I 0 A_SpawnItem("BurnBurnBurn1",1)
			TNT1 A 60 A_KeenDie
			Stop
		Crush:
			CRS2 A -1 A_KeenDie
			Stop
		}
}

//Health was 140; Final glove more powerful for DM/other
ACTOR Bane replaces BaronOfHell
{
	//$Category Reborn Bosses
	Tag "Bane"
	Damage 5
	Health 1400
	Height 64
	Mass 500
	PainChance 150
	Radius 24
	Speed 15
	Scale 1.25
	Monster
	+AMBUSH //inactive till player LOS
	+BOSS
	+FLOORCLIP
	AttackSound "skull/melee"
	DeathSound "brain/cube"
	PainSound "knight/pain"
	Obituary "$BANE"
	States
		{
		Spawn:
			SKEL A 10 A_Look
			Loop
		See:
			CPOS R 0 ACS_Execute (701, 0, 7, 0, 0)
			CPOS R 8
			CPOS S 8 A_PlaySound("bane/see")
			CPOS T 23
			CPOS S 7
			CPOS R 7
			SKEL ABCDEF 4 A_Chase
			Goto See+6
		Pain:
			SPID I 2
			SPID I 2 A_Pain
			Goto See+6
		Melee:
			HEAD B 5 A_FaceTarget
			HEAD C 5 A_FaceTarget
			TROO G 6 A_SkelFist
			HEAD D 6 A_FaceTarget
			Goto See+6
		Missile:
			HEAD B 10 A_FaceTarget
			TROO E 4 A_SkullAttack
			TROO F 25
			Loop
		Death:
			BOSS I 8
			BOSS J 8 A_Scream
			BOSS KLMN 8
			BOSS O 50
			BOSS O 50 A_BrainDie
			Loop
		Burn: //new - this is only seen if you cheat previously, can't combat with flamer
			PLAY H 1 Bright
			PLAY I 0 A_Fall
			PLAY I 0 A_SpawnItem("BurnBurnBurn3",1)
			TNT1 A 60 A_BrainDie
			Stop
		Crush:
			CRS3 A -1 A_BrainDie
			Stop
		}
}

Actor BaneEnd : Bane //with normal Bane endseq will Loop his death state
{
	States
		{
		Death:
			BOSS I 8
			BOSS J 8 A_Scream
			BOSS KLMN 8
			BOSS O 50
			BOSS O 50 A_BrainDie
			Stop
		}
}
//TODO: make bane grow with venom power