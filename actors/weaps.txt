///////////
//WEAPONS//
///////////
ACTOR BatmanFist : DoomWeapon replaces Fist
{
	Game Doom
	Weapon.Kickback 100
	Weapon.SelectionOrder 3700
	+NOALERT
	+WEAPON.MELEEWEAPON
	+WEAPON.WIMPY_WEAPON
	Obituary "$BATFIST"
	States
		{
		Ready:
			PUNG A 1 A_WeaponReady
			Loop
		Deselect:
			PUNG A 1 A_Lower
			Loop
		Select:
			PUNG A 1 A_Raise
			Loop
		Fire:
			PUNG B 4
			PUNG B 4
			PUNG C 0 A_JumpIfInventory("PowerStrength",1,3)
			PUNG C 4 A_CustomPunch (random(1,10)*2, 1)
			PUNG C 0 A_Jump(256, 1)
			PUNG C 4 A_CustomPunch (random(1,10)*20, 1)
			PUNG D 5
			PUNG C 4
			PUNG B 5 A_ReFire
			Goto Ready
		}
}

ACTOR BatmanTazer : DoomWeapon replaces Chainsaw
{
	//$Category Reborn Weapons
	Tag "Tazer"
	Game Doom
	Weapon.Kickback 0
	Weapon.ReadySound "weapons/sawidle"
	Weapon.SelectionOrder 2200
	Weapon.UpSound "weapons/sawup"
	+BLOODLESSIMPACT
	+NOALERT
	+WEAPON.MELEEWEAPON
	SpawnID 161
	Inventory.PickupMessage "$TAZER"
	Obituary "$TAZEROB"
	States
		{
		Ready:
			SAWG CD 4 A_WeaponReady
			Loop
		Deselect:
			SAWG C 1 A_Lower
			Loop
		Select:
			SAWG CCCCDDDD 1 A_Raise
			Loop
		Fire:
			SAWG A 0 A_AlertMonsters(256)
			TNT1 A 0 A_SetBlend("Cyan",0.050,5)
			SAWG AB 4 A_Saw
			SAWG B 0 A_ReFire
			Goto Ready
		Spawn:
			CSAW A -1
			Stop
		}
}

ACTOR Batarang replaces Rocket //probably shaders & rotsprite would be better
{
	//$Category Reborn Ammo
	Tag "Batarang"
	Game Doom
	Radius 11
	Height 8
	Speed 22
	Damage 8
	Projectile
	+RANDOMIZE
	SpawnID 68
	SeeSound "vile/raise"
	DeathSound "baby/attack"
	Obituary "$BRANGOB1"
	States
		{
		Spawn:
			PLSS A 4
			POL3 B 4
			PLSS B 4
			POL3 A 4
			Loop
		Death:
			TNT1 A 0 BRIGHT A_SpawnItemEx("OIS_Spawner",0,0,random(24,32),0,0,0,random(0,360))
			BAL2 C 8 bright
			BAL2 D 6 bright
			BAL2 E 4 bright
			Stop
		}
}

ACTOR BatmanBatarang : DoomWeapon replaces Pistol
{
	//$Category Reborn Weapons
	Tag "Batarangs"
	Game Doom
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	+WEAPON.WIMPY_WEAPON
	SpawnID 162
	Inventory.Pickupmessage "$BRANGS"
	Obituary "$BRANGOB2"
	States
		{
		Ready:
			PISG A 1 A_WeaponReady
			Loop
		Deselect:
			PISG A 1 A_Lower
			Loop
		Select:
			PISG A 1 A_Raise
			Loop
		Fire:
			PISG A 3
			PISG B 3 A_Light0
			PISG C 3
			PISG D 3 A_FireCustomMissile("Batarang", 0, 1, 0, 12)
			PISG E 10 A_Light0
			BON1 D 3
			BON1 C 10 A_ReFire
			Goto Ready
		Spawn:
			PIST A -1
			Stop
		}
}

ACTOR BatmanBlastgun : DoomWeapon replaces BlueCard
{
	//$Category Reborn Weapons
	Tag "BlastGun"
	Game Doom
	Weapon.SelectionOrder 1300
	Weapon.AmmoUse 2
	Weapon.AmmoGive 6
	Weapon.AmmoType "Shell"
	SpawnID 163
	Inventory.PickupMessage "$BLASTGUN"
	Obituary "$BLASTOB"
	States
		{
		Ready:
			SHTG A 1 A_WeaponReady
			Loop
		Deselect:
			SHTG A 1 A_Lower
			Loop
		Select:
			SHTG A 1 A_Raise
			Loop
		Fire:
			SHTF A 5
			TNT1 A 0 A_SetBlend("Yellow",0.050,5)
			SHTF B 7 A_FireShotgun2
			PISF A 5
			SHTG BC 5
			SHTG D 5 A_CheckReload
			COL5 A 35
			COL5 A 1 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 5 BRIGHT A_Light1
			SHTG E 0 BRIGHT A_Light0
			Goto LightDone
		Spawn:
			SHOT A -1
			Stop
		}
}

ACTOR BatmanAzraelGlove : DoomWeapon replaces SuperShotgun
{
	//$Category Reborn Weapons
	Tag "Azrael's Glove"
	Game Doom
	Weapon.SelectionOrder 400
	Weapon.AmmoUse 1
	Weapon.AmmoGive 6
	Weapon.AmmoType "Shell"
	SpawnID 164
	Inventory.PickupMessage "$GLOVES"
	Obituary "$GLOVESOB"
	States
		{
		Ready:
			SHT2 A 1 A_WeaponReady
			Loop
		Deselect:
			SHT2 A 1 A_Lower
			Loop
		Select:
			SHT2 A 1 A_Raise
			Loop
		Fire:
			SHT2 A 4
			SHT2 A 0 A_CheckReload
			SHT2 I 0 A_FireShotgun
			SHT2 I 4 BRIGHT A_OpenShotgun2
			SHT2 J 4 Bright
			SMBT A 4
			SMBT B 0 BRIGHT A_FireShotgun
			SMBT B 4 BRIGHT A_OpenShotgun2
			SMBT C 4 Bright
			SMBT D 4
			SHT2 B 4
			SHT2 C 4
			SHT2 D 4 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 3 BRIGHT A_Light1
			Goto LightDone
		Spawn:
			SGN2 A -1
			Stop
		}
}

ACTOR BatmanChaingun : DoomWeapon replaces Chaingun
{
	//$Category Reborn Weapons
	Tag "BatChaingun"
	Game Doom
	Weapon.SelectionOrder 700
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	SpawnID 165
	Inventory.PickupMessage "$SHURIKEN" //fixme what
	Obituary "$SHURIOB"
	States
		{
		Ready:
			CHGG A 1 A_WeaponReady
			Loop
		Deselect:
			CHGG A 1 A_Lower
			Loop
		Select:
			CHGG A 1 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_SetBlend("Yellow",0.035,5)
			CHGG BB 6 A_FireCGun
			CHGG B 0 A_ReFire
			Goto Ready
		Flash:
			CHGF A 0 BRIGHT A_Light1
			CHGF A 1 Bright
			CHGF A 1 BRIGHT A_LoadShotgun2
			CHGF B 2 Bright
			SMGT B 2 Bright
			SMGT B 0 BRIGHT A_Light0
			Goto LightDone
		Spawn:
			MGUN A -1
			Stop
		}
}

ACTOR SmokeBomb replaces BFGBall
{
	Game Doom
	Radius 13
	Height 8
	Speed 35
	Damage 20
	Projectile
	-NOGRAVITY
	+BLOODLESSIMPACT
	+OLDRADIUSDMG
	+RANDOMIZE
	SpawnID 69
	Obituary "$SMOKEOB"
	DeathSound "weapons/bfgx"
	States
		{
		Spawn:
			BFS1 A -1
			Loop
		Death:
			TNT1 A 0 A_Explode(128)
			TNT1 A 0 A_NoGravity
			TNT1 A 0 BRIGHT A_SpawnItemEx("BoomPuff",0,0,random(24,32),0,0,0,random(0,360))
			TFOG ABCDEFGHI 4 A_FadeOut(0.06)
			Stop
		}
}

ACTOR BatmanSmokeBomb : DoomWeapon replaces RocketLauncher
{
	//$Category Reborn Weapons
	Tag "Smoke Bomb"
	Game Doom
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 10
	Weapon.AmmoGive 20
	Weapon.AmmoType "RocketAmmo"
	+WEAPON.EXPLOSIVE
	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	SpawnID 166
	Inventory.PickupMessage "$SMOKEBMB"
	States
		{
		Ready:
			MISG A 1 A_WeaponReady
			Loop
		Deselect:
			MISG A 1 A_Lower
			Loop
		Select:
			MISG A 1 A_Raise
			Loop
		Fire:
			MISF A 5
			MISF BC 5
			MISF D 5 A_FireCustomMissile("SmokeBomb", 0, 1, 0, 12)
			SHT2 E 5
			SHT2 F 5
			SHT2 G 5 A_ReFire
			Goto Ready
		Spawn:
			LAUN A -1
			Stop
		}
}

ACTOR BatFlame : Flame replaces PlasmaBall
{
	DamageType "Fire"
	SpawnID 70
	States
		{
		Spawn:
			SMRT BCD 3
			SKUL HJ 3
			Stop
		Death:
			TNT1 A 0 BRIGHT A_SpawnItemEx("OIS_Spawner",0,0,random(24,32),0,0,0,random(0,360))
			TGRN CD 3 BRIGHT
			Stop
		}
}

ACTOR BatmanFlamer : DoomWeapon replaces PlasmaRifle
{
	//$Category Reborn Weapons
	Tag "Flamer"
	Game Doom
	Weapon.SelectionOrder 100
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Cell"
	SpawnID 167
	Inventory.PickupMessage "$BFLAMER"
	States
		{
		Ready:
			PLSG A 1 A_WeaponReady
			Loop
		Deselect:
			PLSG A 1 A_Lower
			Loop
		Select:
			PLSG A 1 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_SetBlend("Orange",0.050,5)
			BFLM DC 3 A_FireCustomMissile("BatFlame", 0, 1, 0, 12)
			PLSG B 10 A_ReFire
			Goto Ready
		Spawn:
			PLAS A -1
			Stop
		}
}

ACTOR BatmanFinalGlove : DoomWeapon
{
	Game Doom
	Height 20
	Weapon.SelectionOrder 2800
	Weapon.Kickback 100
	+NOALERT
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.MELEEWEAPON
	+WEAPON.WIMPY_WEAPON
	Obituary "$ENDGLOVE"
	States
		{
		Ready:
			BFGG A 1 A_WeaponReady
			Loop
		Deselect:
			BFGF A 50
			BFGF A -1 A_BFGSound
			Loop
		Select:
			BFGG A 1 A_Raise
			Loop
		Fire:
			BFGG B 5 A_Light0
			BFGG C 5 A_Light0
			SMIT A 6 A_CustomPunch(random(1,10)*20, 1)
			BFGG C 5 A_Light0
			BFGG B 6 A_ReFire
			Goto Ready
		Spawn:
			TNT1 A -1
			Stop
		}
}

ACTOR BatmanFinalGloveItem : CustomInventory replaces BFG9000
{
	//$Category Reborn Weapons
	Tag "Final Glove"
	Game Doom
	Height 20
	SpawnID 31
	SpawnID 168
	Inventory.PickupSound ""
	Inventory.PickupMessage ""
	States
		{
		Pickup:
			TNT1 A 0 A_JumpIfInventory ("BatmanFinalGlove", 1, "Set")
			TNT1 A 0 A_PlaySound ("misc/w_pkup")
			TNT1 A 0 A_PrintBold("$PREPARE", 3.0, "DBIGFONT")
			TNT1 A 0 A_TakeInventory ("BatmanFist")
			TNT1 A 0 A_TakeInventory ("BatmanTazer")
			TNT1 A 0 A_TakeInventory ("BatmanBatarang")
			TNT1 A 0 A_TakeInventory ("BatmanBlastgun")
			TNT1 A 0 A_TakeInventory ("BatmanAzraelGlove")
			TNT1 A 0 A_TakeInventory ("BatmanChaingun")
			TNT1 A 0 A_TakeInventory ("BatmanSmokeBomb")
			TNT1 A 0 A_TakeInventory ("BatmanFlamer")
		Set:
			TNT1 A 0 A_GiveInventory ("BatmanFinalGlove")
			TNT1 A 0 A_SelectWeapon("BatmanFinalGlove")
			TNT1 A 0
			Fail	//forces weapon to stay on ground always
		Spawn:
			BFUG A -1
			Stop
		}
}