/////////
//TRAPS//
/////////
ACTOR ExplosionWallBlast
{
	+BRIGHT
	+NOGRAVITY
	SpawnID 91
	States
		{
		Spawn:
			MISL B 3
			"####" C 3 A_Explode(128)
			"####" D 3
			APBX AB 3
			Stop
		}
}

ACTOR ExplosionWall replaces BossEye //only used on level 22 (uses special functions)
{
	//$Category Reborn Traps
	Tag "Wall Explosion (Map22)"
	Health 10000
	Radius 9
	Height 56
	Mass 1000
	Speed 15
	PainChance 255
	Monster
	+NOCLIP
	+NOGRAVITY
	+SHOOTABLE
	SpawnID 92
	Obituary "$EXPWALL"
	States
		{
		Spawn:
			POL5 A 10 A_Look
			Loop
		See:
			POL5 A 1
		Pain:
			POL5 A 5 A_PlaySound("weapons/rocklx", 0, 1.0, 0, ATTN_NONE)
			"####" A 1 Teleport(0, 22, 0)
			"####" A 1 A_Recoil (-7)
			"####" A 1 A_UnSetShootable
		Chase:
			POL5 AAAAAAAAAAAAAA 0 A_SpawnItemEx("ExplosionWallBlast", 128, random(-512,512), random(0,512), 0, 0, 0, 0, 0, 0)
			"####" A 1 A_PlaySound("weapons/rocklx", 0, 1.0, 0, ATTN_NONE)
			"####" AAAAAAAAAAAAAA 0 A_SpawnItemEx("ExplosionWallBlast", 128, random(-512,512), random(0,512), 0, 0, 0, 0, 0, 0)
			"####" A 0 A_Stop
			"####" A 1 A_Recoil (-8)
			"####" AAAAAAAAAAAAAA 0 A_SpawnItemEx("ExplosionWallBlast", 128, random(-512,512), random(0,512), 0, 0, 0, 0, 0, 0)
			"####" A 0 A_Stop
			"####" A 1 A_Recoil (-7)
			"####" AAAAAAAAAAAAAA 0 A_SpawnItemEx("ExplosionWallBlast", 128, random(-512,512), random(0,512), 0, 0, 0, 0, 0, 0)
			"####" A 0 A_Stop
			"####" A 1 A_Recoil (-8)
			"####" AAAAAAAAAAAAAA 0 A_SpawnItemEx("ExplosionWallBlast", 128, random(-512,512), random(0,512), 0, 0, 0, 0, 0, 0)
			"####" A 0 A_Stop
			"####" A 1 A_Recoil (-7)
			Goto Chase
		}
}

ACTOR HateBullet
{
	Game Doom
	Radius 13
	Height 8
	Speed 100
	Damage 1
	Projectile
	+BLOODLESSIMPACT
	RenderStyle Add
	Alpha 0.0
	states
		{
		Spawn:
			TLP2 AAAA 1
			Stop
		Death:
			FIRE F 1
			Stop
		}
}

ACTOR OneShotCauseHateTarget replaces HangBNoBrain
{
	//$Category Reborn Traps
	Tag "OneShotCauseHateTarget"
	Health 1000
	Mass 1000
	Radius 2
	Height 2
	Speed 0
	+SHOOTABLE
	SpawnID 93
	States
		{
		Spawn:
			POL5 A 5 A_Look
			Loop
		See:
			BOSS EF 7 A_FaceTarget
			BOSS GGGGGG 1 A_CustomMissile("HateBullet",32,0,0,CMF_AIMDIRECTION)
			TNT1 AAAAAA 3 A_Chase
			Goto See+12
		}
}

ACTOR FloorFireTrap replaces Megasphere
{
	//$Category Reborn Traps
	Tag "Floor Fire Trap"
	Game Doom
	SpawnID 94
	SeeSound "wolfss/sight"
	States
		{
		Spawn:
			POL5 A 10 A_Look
			Loop
		See:
			MEGA ABCD 3 Bright A_CustomMeleeAttack(random(1,8))
			PLSE ABC 3 Bright A_CustomMeleeAttack(random(1,8))
			POL5 A 10 Bright A_Jump(64,1)
			POL5 A 10 Bright A_Jump(64,1)
			POL5 A 10 Bright A_Jump(64,1)
			POL5 A 10 Bright A_Jump(64,1)
			POL5 A 10 Bright
			Goto Spawn
		}
}

ACTOR FireShootTrap replaces GreenTorch
{
	//$Category Reborn Traps
	Tag "Fire Shooting Trap"
	Game Doom
	Health 7000000 //??
	Radius 24
	Height 64
	Mass 1000000
	PainChance 255
	+NOBLOOD
	+SHOOTABLE
	+SOLID
	SpawnID 95
	States
		{
		Spawn:
			POL5 A 10 A_Look
			Loop
		See:
			TLMP ABC 4 Bright A_CustomMeleeAttack(random(1,8))
			TLMP A 0 Bright A_Jump(128, "Melee")
			BFE1 ABC 4 Bright A_CustomMeleeAttack(random(1,8))
			BFE1 A 0 Bright A_Jump(128, "Missile")
			Loop
		Melee:
			BFE1 AB 4 Bright A_CustomMeleeAttack(random(1,8))
			BFE1 C 4 Bright A_CustomMissile("ArachnotronPlasma",32,0,0,CMF_AIMDIRECTION)
			Goto See
		Missile:
			TLMP AB 4 Bright A_CustomMeleeAttack(random(1,8))
			TLMP C 4 Bright A_CustomMissile("ArachnotronPlasma",32,0,0,CMF_AIMDIRECTION)
			Goto See+4
		}
}

ACTOR LandMine replaces RedTorch
{
	//$Category Reborn Traps
	Tag "Land Mine"
	Game Doom
	Health 1
	Radius 6
	Height 6
	Mass 1000000
	+NOBLOOD
	+OLDRADIUSDMG
	+SHOOTABLE
	SpawnID 96
	DeathSound "weapons/rocklx"
	States
		{
		Spawn:
			POL4 A 10 A_Look
			Loop
		See:
			POL4 A 2 A_Chase
			Loop
		Melee:
		Death:
			POSS M 5 Bright A_Scream
			"####" N 2 Bright A_Explode
			"####" OPQRSTU 5 Bright
			Stop
		}
}

ACTOR Explosion replaces Cacodemon //used to kill stuff immediatly on seeing player
{
	//$Category Reborn Specials
	Tag "PreKiller"
	Game Doom
	Radius 16
	Height 16
	+INVISIBLE
	+OLDRADIUSDMG
	SpawnID 97
	States
		{
		Spawn:
			POL5 A 10 A_Look
			Loop
		See:
			FIRE B 2 A_Explode(128)
			Stop
		}
}

ACTOR ScareCrowPopup replaces Gibs
{
	//$Category Reborn Specials
	Tag "Scarecrow PopUp"
	Game Doom
	Height 63
	Radius 20
	SpawnID 98
	SeeSound "skeleton/sight"
	States
		{
		Spawn:
			POL5 A 5 A_Look
			Loop
		See:
			TRPS A 0 Bright A_CustomMissile("PoisonCloud",16,0,0,2,0)
			"####" AABBCCDDEEFFGGCCDDEEFFGGCCDDEFFGGCCDDEEFFGGCCDDEEFFGGCCDDEEFFEDC 0 Bright A_SpawnItemEx ("PoisonFartCloud",random(-64,64),random(-64,64),random(-48,48),0,0,0.1,0,128)
			"####" ABCDEFGCDEFGCDEFGCDEFGCDEFGCDEFG 0 Bright A_SpawnItemEx ("TrickOrTreatSmoke",random(-64,64),random(-64,64),random(-48,48),momx,0,frandom(0.5,1),0,SXF_TRANSFERTRANSLATION,160)
			BKEY B 3 Bright
			RKEY B 3 Bright
			YKEY B 3 Bright
			SMGT C 3 Bright
			"####" D 3 Bright
			SCRC A 3 Bright A_SkelMissile
			SMGT D 3 Bright
			"####" C 3 Bright
			YKEY B 3 Bright
			RKEY B 3 Bright
			BKEY B 3 Bright
			"####" B 3 Bright A_FadeOut(0.07)
			Stop
		}
}

ACTOR FloorFire replaces TallRedColumn
{
	//$Category Reborn Traps
	Tag "Floor Fire"
	Game Doom
	SpawnID 99
	SeeSound "brain/pain"
	DeathSound "brain/pain"
	States
		{
		Spawn:
			POL5 A 10 A_Look
			Loop
		See:
			TLMP ABC 4 Bright
		Burn:
			BFE1 ABC 4 Bright A_CustomMeleeAttack(random(1,12))
			TNT1 A 0 Bright A_LookEx(0,0,256,0,256,"Burn")
			Goto Burn
		}
}