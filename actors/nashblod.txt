// Modify the internal Blood actor to spawn flying blood
ACTOR NashGore_Blood : Blood replaces Blood
{
	Game Doom
	States
		{
		Spawn:
			BLUD C 8
			TNT1 A 0 bright A_SpawnItemEx("OIS_Spawner",0,0,random(24,32),0,0,0,random(0,360))
			BLUD B 8 A_SpawnDebris("NashGore_FlyingBlood",1)
			"####" A 8
			Stop
		}
}

// Blood sprite that flies outwards and leaves trails behind
ACTOR NashGore_FlyingBlood
{
	Game Doom
	Scale 0.75
	Health 1
	Radius 8
	Height 1
	Mass 128
	+CORPSE
	+NOBLOCKMAP
	+NOTELEPORT
	States
		{
		Spawn:
			TNT1 A 0
			FBLD A 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
			"####" A 1
			"####" A 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
			"####" A 1
			"####" B 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
			"####" B 1
			"####" B 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
			"####" B 1
			"####" C 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
			"####" C 1
			"####" C 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
			"####" C 1
			"####" D 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
			"####" D 1
			"####" D 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
			"####" D 1
			Loop
		Crash:
			TNT1 A 1 A_SpawnItem("NashGore_BloodSpot",0,0,0,1)
			Stop
		}
}

ACTOR NashGore_BloodSpot
{
	Game Doom
	Radius 12
	Height 2
	Mass 128
	-NOBLOCKMAP
	+NOTELEPORT
	states
		{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(waterlevel > 0, "Underwater") //..so it will lag no more...
			TNT1 A 0 A_PlaySound("bloodsplat")
			TNT1 A 0 A_Jump(64,4)
			TNT1 A 0 A_Jump(128,4)
			TNT1 A 0 A_Jump(192,4)
			TNT1 A 0 A_Jump(255,4)
			// make the blood spots stay on the ground. 1500 is the default duration.
			BSPT A 2500 //1500
			stop
			BSPT B 2500 //1500
			stop
			BSPT C 2500 //1500
			stop
			BSPT D 2500 //1500
			stop
		Underwater:	//..so it will lag no more...
			TNT1 A 0 A_PlaySound("bloodsplat")
			TNT1 A 0 A_Jump(64,4)
			TNT1 A 0 A_Jump(128,4)
			TNT1 A 0 A_Jump(192,4)
			TNT1 A 0 A_Jump(255,4)
			// now the bloodspots will remain underwater only for a short time, then vanishes
			BSPT A 250
			stop
			BSPT B 250
			stop
			BSPT C 250
			stop
			BSPT D 250
			stop
		}
}

// Blood trails spawned by FlyingBlood
ACTOR NashGore_FlyingBloodTrail
{
	Game Doom
	Scale 0.70
	Mass 1
	+LOWGRAVITY
	+NOBLOCKMAP
	+NOTELEPORT
	States
		{
		Spawn:
			BTRL A 4
			"####" B 4
			"####" C 4
			"####" D 4
			Stop
		}
}